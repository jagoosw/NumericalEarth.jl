using ClimaSeaIce.SeaIceThermodynamics: melting_temperature
using ClimaSeaIce.SeaIceThermodynamics: LinearLiquidus
using NumericalEarth.EarthSystemModels
using NumericalEarth.EarthSystemModels: NoSeaIceInterface
using NumericalEarth.EarthSystemModels.InterfaceComputations

#####
##### A workaround when you don't have a sea ice model
#####

struct FreezingLimitedOceanTemperature{L}
    liquidus :: L
end

"""
    FreezingLimitedOceanTemperature(FT=Float64; liquidus=LinearLiquidus(FT))

The minimal possible sea ice representation, clipping the temperature below to the freezing point.
Not really a "model" per se, however, it is the most simple way to make sure that temperature
does not dip below freezing.

The melting temperature is a function of salinity and is controlled by the `liquidus`.
"""
FreezingLimitedOceanTemperature(FT::DataType=Oceananigans.defaults.FloatType; liquidus=LinearLiquidus(FT)) =
    FreezingLimitedOceanTemperature(liquidus)

const FreezingLimitedEarthSystemModel = EarthSystemModel{<:FreezingLimitedOceanTemperature, A, O, <:NoSeaIceInterface} where {A, O}

# Extend interface methods to work with a `FreezingLimitedOceanTemperature`
sea_ice_concentration(::FreezingLimitedOceanTemperature) = ZeroField()
sea_ice_thickness(::FreezingLimitedOceanTemperature) = ZeroField()

# does not matter
reference_density(::FreezingLimitedOceanTemperature) = 0
heat_capacity(::FreezingLimitedOceanTemperature) = 0
time_step!(::FreezingLimitedOceanTemperature, Δt) = nothing

# FreezingLimitedOceanTemperature handles temperature limiting in compute_sea_ice_ocean_fluxes!
EarthSystemModels.above_freezing_ocean_temperature!(ocean, grid, ::FreezingLimitedOceanTemperature) = nothing

# No atmosphere-sea ice or sea ice-ocean interface for FreezingLimitedOceanTemperature
InterfaceComputations.default_ai_temperature(::FreezingLimitedOceanTemperature) = nothing
InterfaceComputations.ThreeEquationHeatFlux(::FreezingLimitedOceanTemperature) = nothing
InterfaceComputations.atmosphere_sea_ice_interface(grid, atmos, ::FreezingLimitedOceanTemperature, args...) = nothing
InterfaceComputations.atmosphere_sea_ice_interface(grid, ::Nothing, ::FreezingLimitedOceanTemperature, args...) = nothing
InterfaceComputations.sea_ice_ocean_interface(grid, ::FreezingLimitedOceanTemperature, ocean, flux_formulation; kwargs...) = nothing
InterfaceComputations.sea_ice_ocean_interface(grid, ::FreezingLimitedOceanTemperature, ::Nothing, flux_formulation; kwargs...) = nothing
InterfaceComputations.sea_ice_ocean_interface(grid, ::FreezingLimitedOceanTemperature, ocean, ::ThreeEquationHeatFlux; kwargs...) = nothing
InterfaceComputations.sea_ice_ocean_interface(grid, ::FreezingLimitedOceanTemperature, ::Nothing, ::ThreeEquationHeatFlux; kwargs...) = nothing

InterfaceComputations.net_fluxes(::FreezingLimitedOceanTemperature) = nothing

const OnlyOceanwithFreezingLimited      = EarthSystemModel{<:FreezingLimitedOceanTemperature, <:Nothing, <:Any}
const OnlyAtmospherewithFreezingLimited = EarthSystemModel{<:FreezingLimitedOceanTemperature, <:Any,     <:Nothing}
const SingleComponentPlusFreezingLimited = Union{OnlyAtmospherewithFreezingLimited, OnlyOceanwithFreezingLimited}

# Also for the ocean nothing really happens here
EarthSystemModels.update_net_fluxes!(::SingleComponentPlusFreezingLimited, ocean::Simulation{<:HydrostaticFreeSurfaceModel}) = nothing

# No need to compute fluxes for this "sea ice model"
InterfaceComputations.compute_atmosphere_sea_ice_fluxes!(cm::FreezingLimitedEarthSystemModel) = nothing

# Same for the sea_ice ocean fluxes
function InterfaceComputations.compute_sea_ice_ocean_fluxes!(cm::FreezingLimitedEarthSystemModel)
    ocean = cm.ocean
    liquidus = cm.sea_ice.liquidus
    grid = ocean.model.grid
    arch = architecture(grid)
    Sᵒᶜ = ocean.model.tracers.S
    Tᵒᶜ = ocean.model.tracers.T

    launch!(arch, grid, :xyz, _above_freezing_ocean_temperature!, Tᵒᶜ, Sᵒᶜ, liquidus)

    return nothing
end

@kernel function _above_freezing_ocean_temperature!(Tᵒᶜ, Sᵒᶜ, liquidus)

    i, j, k = @index(Global, NTuple)

    @inbounds begin
        Sᵏ = Sᵒᶜ[i, j, k]
        Tᵏ = Tᵒᶜ[i, j, k]
    end

    Tₘ = melting_temperature(liquidus, Sᵏ)
    @inbounds Tᵒᶜ[i, j, k] = ifelse(Tᵏ < Tₘ, Tₘ, Tᵏ)
end

#####
##### Chekpointing (not needed for FreezingLimitedOceanTemperature)
#####

import Oceananigans: prognostic_state, restore_prognostic_state!

prognostic_state(::FreezingLimitedOceanTemperature) = nothing
restore_prognostic_state!(flt::FreezingLimitedOceanTemperature, state) = flt
restore_prognostic_state!(flt::FreezingLimitedOceanTemperature, ::Nothing) = flt
